# CreateLibraryInternalIceTest - creates a test that uses a Test ice file
#
# params: 
# test_name - the directory name of test/<name>
# include_dirs - include directories needed for the target
# output_dir - the directory the built files should go in
# test_sources - the test source files with path
# ice_files - ice files generated by the test with path
# lib_target - name of the server target
# lib_name - name of the output file
# test_deps - the targets or libraries the test depends on

function(CreateLibraryInternalIceTest test_name include_dirs output_dir test_sources ice_files lib_target lib_name test_deps)
	get_filename_component(test_dir_name ${CMAKE_CURRENT_SOURCE_DIR} NAME)
	
	if(NOT ${output_dir}/${test_name})
		file(MAKE_DIRECTORY ${output_dir}/${test_name})
	endif()

	list(APPEND ICE_SLICE_DIRS "-I${CMAKE_CURRENT_SOURCE_DIR}")
	list(APPEND ICE_SLICE_PARAMS ${COMPILE_SLICES_CPP_PARAMETERS} ${test_name})

	foreach(slice IN LISTS ice_files)
		get_filename_component(slice_name ${slice} NAME_WE)

		list(APPEND GENERATED_LIBINTICE_HEADERS 
			${output_dir}/${test_name}/${slice_name}.h
		)

		list(APPEND GENERATED_LIBINTICE_SOURCES 
			${output_dir}/${test_name}/${slice_name}.cpp)

		#function(CompileSlice slice2bin_params slice_include_paths ice_files output_dir output_file)
		CompileSlice("${ICE_SLICE_PARAMS}"
			"${ICE_SLICE_DIRS}"
			${slice}
			${output_dir}/${test_name}
		)
	endforeach()

	add_library(${lib_target})

	target_sources(${lib_target} 
		PRIVATE 
			${test_sources}
			${GENERATED_LIBINTICE_HEADERS}
			${GENERATED_LIBINTICE_SOURCES}
	)

	target_include_directories(${lib_target}
		PRIVATE
			${include_dirs}
	)

	if(BUILD_ICE_CPP11)
		set_property(TARGET ${lib_target} PROPERTY CXX_STANDARD 11)
	endif()

	target_link_libraries(${lib_target} PRIVATE ${test_deps})

	target_compile_definitions(${lib_target} PRIVATE ${ICE_COMPILE_DEFS})

	if(MSVC)
		source_group("Slice Files"
			FILES
				${ice_files}
		)

		source_group("Source Files/Generated"
			FILES
				${GENERATED_LIBINTICE_SOURCES}
		)

		source_group("Header Files/Generated"
			FILES
				${GENERATED_LIBINTICE_HEADERS}
		)

		target_compile_options(${lib_target}
			PRIVATE
				${ICE_MSVC_COMPILE_OPTIONS}
		)

		set_property(TARGET ${lib_target} PROPERTY LINK_FLAGS ${ICE_MSVC_DLL_LINK_OPTIONS})
	endif()

	if(BUILD_ICE_CPP11)
		set_target_properties(${lib_target}
			PROPERTIES
				OUTPUT_NAME ${lib_name}${zeroc-ice_VERSION_MAJOR}${zeroc-ice_VERSION_MINOR}++11
				CXX_STANDARD 11
		)
	else()
		set_property(TARGET ${lib_target}
			PROPERTY
				OUTPUT_NAME ${lib_name}${zeroc-ice_VERSION_MAJOR}${zeroc-ice_VERSION_MINOR}
		)
	endif()

	if(BUILD_SHARED_LIBS)
		set_property(TARGET ${lib_target} PROPERTY RUNTIME_OUTPUT_DIRECTORY ${output_dir}/bin)
	else()
		set_property(TARGET ${lib_target} PROPERTY LIBRARY_OUTPUT_DIRECTORY ${output_dir}/lib)
	endif()
endfunction()
